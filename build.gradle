plugins {
    id 'fabric-loom' version '1.14-SNAPSHOT'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
    id 'maven-publish'
}

version = "$project.mod_version+$project.minecraft_version"
group = project.maven_group

sourceSets {
    data {
        compileClasspath += main.compileClasspath
        runtimeClasspath += main.runtimeClasspath
    }

    main {
        resources.srcDir 'src/generated/resources'
    }
}

loom {
    splitEnvironmentSourceSets()
    accessWidenerPath = file('src/main/resources/woods_and_mires.accesswidener')

    mods {
        woods_and_mires {
            sourceSet 'main'
            sourceSet 'client'
        }
    }
}

fabricApi {
    configureDataGeneration() {
        client = true
    }
}

repositories {
    maven {
        url = "https://maven.minecraftforge.net"
    }

    maven {
        url = "https://maven.terraformersmc.com/releases/"
        content {
            includeGroup "com.terraformersmc.terraform-api"
        }
    }
    maven { url 'https://maven.nucleoid.xyz' }
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation("com.terraformersmc.terraform-api:terraform-wood-api-v1:${project.terraform_api_version}")

    modImplementation "com.github.glitchfiend:TerraBlender-fabric:$project.terrablender_version"
    implementation include("com.typesafe:config:$project.typesafe_config_version")

    modImplementation "eu.pb4:polymer-resource-pack:$project.polymer_version"
    modImplementation "eu.pb4:polymer-resource-pack-extras:$project.polymer_version"

    // Used for sorting features in the data generator
    implementation "org.jgrapht:jgrapht-core:$project.jgrapht_version"
}

publishMods {
    file = remapJar.archiveFile
    type = STABLE
    changelog = providers.environmentVariable("CHANGELOG").getOrElse("No changelog provided")

    displayName = "WoodsAndMires ${version.get()}"

    github {
        accessToken = providers.environmentVariable("GITHUB_TOKEN")
        repository = providers.environmentVariable("GITHUB_REPOSITORY").getOrElse("KiloCraft/WoodsAndMires")
        commitish = providers.environmentVariable("GITHUB_REF_NAME").getOrElse("main")
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.release = 21
}

jar {
    from(["COPYING", "COPYING.LESSER"]) {
        into 'META-INF'
    }
    exclude '.cache'
}

sourcesJar {
    from(["COPYING", "COPYING.LESSER"]) {
        into 'META-INF'
    }
    exclude '.cache'
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}
